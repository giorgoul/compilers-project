/**
 *  Package and Import Specifications
 */
import java_cup.runtime.*;
import java.io.*;

/**
 *  Usercode Components
 */
parser code {:
    // Connect this parser to a scanner!
    // Also create the Translated.ir file to be accessed by each action.
    ScannerIR s;
    BufferedWriter ir;
    ParserIR(ScannerIR s) { 
        this.s=s;
        try {
            ir = new BufferedWriter(new FileWriter("Translated.ir"));
        } catch (IOException e) {
            System.out.println("Error creating Translated.ir");
        }
    }
:}

/* define how to connect to the scanner! */
scan with {: return s.next_token(); :};

/**
 *  Symbol Lists
 */

/* Terminals (tokens returned by the scanner). */
terminal            PLUS, LPAREN, RPAREN, FUNCOPEN, RBRACK, IF, ELSE, EQUAL, PREFIX, SUFFIX, COMMA, REVERSE;
terminal String     STRING_LITERAL, IDENTIFIER;

/* Non terminals */
non terminal String            program_start, program, func, args, args2, func_body, func_call, func_call_func, params, params_func, expr_func, if_else_stmt_func, if_else_body_func, func_decls, program_tail, program_tails;

start with program_start;

/**
 *  The Grammar Rules
 */

program_start ::= program:p                                                         {: ir.write(p); ir.flush(); ir.close(); :};
/* Add eval(x) function for easier handling of top-level expressions */
program ::= func_decls:fds program_tails:pts                                        {: RESULT = fds + "eval(x){x}" + pts; :};
program_tails ::= program_tail:pt program_tails:pts                                 {: RESULT = "eval(" + pt + ")" + pts; :}
                | program_tail:pt                                                   {: RESULT = "eval(" + pt + ")"; :}
                ;
program_tail ::= REVERSE program_tail:pt                                            {: RESULT = "reverse " + pt; :}
                | STRING_LITERAL:str PLUS program_tail:pt                           {: RESULT = "\"" + str + "\"" + "+" + pt; :}
                | STRING_LITERAL:str                                                {: RESULT = "\"" + str + "\""; :}
                | func_call:fc PLUS program_tail:pt                                 {: RESULT = fc + "+" + pt; :}
                | func_call:fc                                                      {: RESULT = fc; :}
                ;
func_decls ::= func_decls:fds func:f                                                {: RESULT = fds + f; :}
            | func:f                                                                {: RESULT = f; :}
            ;
func ::= IDENTIFIER:id LPAREN args:a FUNCOPEN func_body:b RBRACK                    {: RESULT = id + "(" + a + ")" + "{" + b + "}"; :};
func_body ::= expr_func:e                                                           {: RESULT = e; :}
            | if_else_stmt_func:if_else                                             {: RESULT = if_else; :}
            ;
args ::= IDENTIFIER:id COMMA args2:a2                                               {: RESULT = id + "," + a2; :}
        | IDENTIFIER:id2                                                            {: RESULT = id2; :}
        |                                                                           {: RESULT = ""; :}
        ;
args2 ::= IDENTIFIER:id COMMA args2:a2                                              {: RESULT = id + "," + a2; :}
        | IDENTIFIER:id2                                                            {: RESULT = id2; :}
        ;
func_call ::= IDENTIFIER:id LPAREN params:p RPAREN                                  {: RESULT = id + "(" + p + ")"; :};
func_call_func ::= IDENTIFIER:id LPAREN params_func:p RPAREN                        {: RESULT = id + "(" + p + ")"; :};
params ::= STRING_LITERAL:str COMMA params:p                                        {: RESULT = "\"" + str + "\"" + "," + p; :}
        | func_call:fc COMMA params:p                                               {: RESULT = fc + "," + p; :}
        | STRING_LITERAL:str                                                        {: RESULT = "\"" + str + "\""; :}                                                    
        | func_call:fc                                                              {: RESULT = fc; :}
        |                                                                           {: RESULT = ""; :}
        ;
params_func ::= STRING_LITERAL:str COMMA params_func:p                              {: RESULT = "\"" + str + "\"" + "," + p; :}
            | func_call_func:fc COMMA params_func:p                                 {: RESULT = fc + "," + p; :}
            | IDENTIFIER:id COMMA params_func:p                                     {: RESULT = id + "," + p; :}
            | STRING_LITERAL:str                                                    {: RESULT = "\"" + str + "\""; :}
            | func_call_func:fc                                                     {: RESULT = fc; :}
            | IDENTIFIER:id                                                         {: RESULT = id; :}
            |                                                                       {: RESULT = ""; :}
            ;
expr_func ::= REVERSE expr_func:exp                                                 {: RESULT = "reverse " + exp; :}
            | STRING_LITERAL:str                                                    {: RESULT = "\"" + str + "\""; :}
            | IDENTIFIER:id                                                         {: RESULT = id; :}
            | func_call_func:fc                                                     {: RESULT = fc; :}
            | STRING_LITERAL:str PLUS expr_func:exp                                 {: RESULT = "\"" + str + "\"" + "+" + exp; :}
            | IDENTIFIER:id PLUS expr_func:exp                                      {: RESULT = id + "+" + exp; :}
            | func_call_func:fc PLUS expr_func:exp                                  {: RESULT = fc + "+" + exp; :}
            ;
if_else_stmt_func ::= IF LPAREN expr_func:exp1 PREFIX expr_func:exp2 RPAREN if_else_body_func:if_body ELSE if_else_body_func:else_body                  {: RESULT = "if(" + exp1 + " prefix " + exp2 + ")" + if_body + "else " + else_body; :}
                    | IF LPAREN expr_func:exp1 SUFFIX expr_func:exp2 RPAREN if_else_body_func:if_body ELSE if_else_body_func:else_body                  {: RESULT = "if(reverse " + exp1 + " prefix reverse " + exp2 + ")" + if_body + "else " + else_body; :}
                    | IF LPAREN expr_func:exp1 EQUAL expr_func:exp2 RPAREN if_else_body_func:if_body ELSE if_else_body_func:else_body                   {: RESULT = "if(" + exp1 + " prefix " + exp2 + ")" + "if(" + exp2 + " prefix " + exp1 + ")" + if_body + "else " + else_body + "else " + else_body; :}
                    ;
if_else_body_func ::= expr_func:exp                                                                                                                     {: RESULT = exp; :}
                    | if_else_stmt_func:if_else                                                                                                         {: RESULT = if_else; :}
                    ;